plugins {
    id 'java'
    id 'jacoco'
    id 'application'
//    id 'com.diffplug.spotless' version '6.22.0'
    id 'info.solidsoft.pitest' version '1.15.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.vavr:vavr:1.0.0-alpha-4'

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation("com.approvaltests:approvaltests:22.2.0")
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.3")
    testImplementation("org.mockito:mockito-core:5.6.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.6.0")
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.15.6'
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.incremental = true
//    dependsOn 'spotlessApply'
}


application {
    mainClass.set("kata.App")
}

jar {
    manifest {
        attributes "Main-Class": "kata.App"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

project.tasks.check.dependsOn("pitest")
pitest {
    junit5PluginVersion = '1.0.0'
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    targetClasses = ['kata.*']
    pitestVersion = '1.9.11'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}

//spotless {
//    java {
//        googleJavaFormat()
//        formatAnnotations()
//        target project.fileTree(project.rootDir) {
//            include '**/*.java'
//        }
//    }
//}
